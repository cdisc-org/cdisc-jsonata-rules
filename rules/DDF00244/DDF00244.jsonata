{
    "check_func" : function($data){$data.(
        (
            $d := $;
            ($.study.versions)@$sv.
                ($sv.narrativeContentItems[$contains(text,/usdm:ref/)])@$nci.
                    $match( $nci.text,
                            /<usdm:ref([^>]*)(\/>|><\/usdm:ref>)/
                    )@$ref.
                    (
                        $g0_or_null := function($m){$m ? $m.groups[0] : null};
                        {
                            "StudyVersion.id": $sv.id,
                            "StudyVersion.name": $sv.name,
                            "id": $nci.id,
                            "name": $nci.name,
                            "text": $nci.text,
                            "usdm_ref": {
                                            "match": $ref.match,
                                            "klass": $match($ref.groups[0],/klass=\"([a-zA-Z]+)\"/) ~> $g0_or_null(),
                                            "id": $match($ref.groups[0],/id=\"(\w+)\"/) ~> $g0_or_null(),
                                            "attribute": $match($ref.groups[0],/attribute=\"([a-zA-Z]+)\"/) ~> $g0_or_null()
                                        },                            
                            "dataset": $nci.instanceType
                        }
                    )
                    ~>  $map(function($v)
                            {
                                (
                                    $ref_val := "usdm_ref" in $keys($v)
                                                ? $utils.get_ref_value($v.usdm_ref,$d,"!!NOT FOUND!!")
                                                : $v.value;
                                    $v ~> |$|{"Invalid Reference": $v.usdm_ref.match, "Referenced Value": $ref_val},['usdm_ref']|
                                )
                            }
                        )
                    ~>  $filter(function($v)
                            {
                                $v.`Referenced Value` = "!!NOT FOUND!!"
                            }
                        )
        )
    )},
    "message": "The item referenced in the narrative content item text is not available elsewhere in the model.",
    "variables": [
            "StudyVersion.id",
            "StudyVersion.name",
            "id",
            "name",
            "text",
            "Invalid Reference"
        ],
    "scope": {
        "Include": [
            "NarrativeContentItem"
        ]
    }
}