{
    "check_func" : function($data){$data.(
  (
    $lkp:=**[id and instanceType].$each(function($v,$k){{$join([instanceType,id,$k],"|"):$v}})~>$merge;
    $.study.**.dictionaries.parameterMaps.
        {
          "instanceType": instanceType,
          "id": id,
          "path": _path,
          "SyntaxTemplateDictionary.id": %.id,
          "SyntaxTemplateDictionary.name": %.name,
          "tag": tag,
          "reference": reference
        }
    ~>  $map(function($v)
          {
            (
              $ref := $match($v.reference,
                        /<usdm:ref((?=[^>]* klass=\"([a-zA-Z]+)\")(?=[^>]* id=\"(\w+)\")(?=[^>]* attribute=\"([a-zA-Z]+)\")[^>]*)(\/>|><\/usdm:ref>)/
                      )
                      {
                        "value": $v.reference,
                        "usdm_ref": $contains($v.reference,/usdm:ref/) ? 
                                    {
                                      "klass": groups[1],
                                      "id": groups[2],
                                      "attribute": groups[3]
                                    }
                      };
              $v ~> |$|{"reference": $ref}|
            )
          }
        )
    ~>  $map(function($v)
          {
            (
              $ref_val := "usdm_ref" in $keys($v.reference)
                          ? (
                              $k := $join(
                                      [
                                        $v.reference.usdm_ref.klass,
                                        $v.reference.usdm_ref.id,
                                        $v.reference.usdm_ref.attribute
                                      ],"|"
                                    );
                              $k in $keys($lkp)
                              ? $lookup($lkp,$k)
                              : "!!NOT FOUND!!"
                            )
                          : $v.value;
              $v ~> |$|{"reference": $v.reference.value, "Referenced Value": $ref_val}|
            )
          }
        )
    ~>  $filter(function($v)
          {
            $v.`Referenced Value` = "!!NOT FOUND!!"
          }
        )
  )
    )},
    "message": "The item referenced in the parameter map is not available elsewhere in the model.",
    "variables": [
            "SyntaxTemplateDictionary.id",
            "SyntaxTemplateDictionary.name",
            "tag",
            "reference"
        ],
    "scope": {
        "Include": [
            "ParameterMap"
        ]
    }
}