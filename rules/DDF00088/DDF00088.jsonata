{
    "check_func" : function($data){$data.(
  ($.study.versions)@$sv.
      ($sv.studyDesigns)@$sd.
          ($sd.scheduleTimelines[mainTimeline = true])@$st.
              (
                  $actord := $utils.parse_refs("nextId","next",$sd.epochs)
                              [previousId = null].$append(id,**.next.id);
                  $reford := $utils.parse_refs("defaultConditionId","defaultCondition",$st.instances)
                              [id = $st.entryId].**[instanceType="ScheduledActivityInstance" and epochId in $sd.epochs.id].epochId
                              ~> $filter(function($v,$i,$a){$i = 0 or ($i > 0 and $v != $a[$i-1])});
                  $epoch := function($id){($e:=$sd.epochs[id=$id];$id & ": " & $e.name)};
                  {
                      "instanceType": $sd.instanceType,
                      "id": $sd.id,
                      "path": $sd._path,
                      "name": $sd.name,
                      "ScheduleTimeline.id": $st.id,
                      "ScheduleTimeline.name": $st.name,
                      "ScheduleTimeline.mainTimeline": $st.mainTimeline,
                      "Epoch order by previous/next": $actord ~> $map($epoch) ~> $join(" > "),
                      "Epoch order by timeline refs": $reford ~> $map($epoch) ~> $join(" > ")
                  }[`Epoch order by previous/next` != `Epoch order by timeline refs`]
              )
                    )},
    "message": "The order of the epochs in the study design (as defined by the next attribute) does not match the order of epochs as referenced from scheduled activity instances in the main timeline that are ordered by default condition.",
    "variables": [
        "name",
        "ScheduleTimeline.id",
        "ScheduleTimeline.name",
        "ScheduleTimeline.mainTimeline",
        "Epoch order by previous/next",
        "Epoch order by timeline refs"
    ],
    "scope": {
        "Include": [
            "StudyDesign",
            "InterventionalStudyDesign",
            "ObservationalStudyDesign"
        ]
    }
}