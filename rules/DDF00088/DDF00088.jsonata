{
    "check_func" : function($data){$data.(
                        ($.study.versions)@$sv.
                            ($sv.studyDesigns)@$sd.
                                ($sd.scheduleTimelines[mainTimeline = true])@$st.
                                    (
                                        $actord := $utils.parse_refs("nextId","next",$sd.epochs)
                                                    [previousId = null].$append(id,**.next.id);
                                        $reford := $utils.parse_refs("defaultConditionId","defaultCondition",$st.instances)
                                                    [id = $st.entryId].**[instanceType="ScheduledActivityInstance" and epochId in $sd.epochs.id].epochId
                                                    ~> $utils.del_reps;
                                        $epoch := function($id){($e:=$sd.epochs[id=$id];$id & " [" & $e.name & "]")};
                                        {
                                            "id": $sd.id,
                                            "name": $sd.name,
                                            "ScheduleTimeline.id": $st.id,
                                            "ScheduleTimeline.name": $st.name,
                                            "ScheduleTimeline.mainTimeline": $st.mainTimeline,
                                            "Epoch order": $actord ~> $map($epoch),
                                            "Referenced epoch order": $reford ~> $map($epoch),
                                            "dataset": $sd.instanceType
                                        }[`Epoch order` != `Referenced epoch order`]
                                    )
                    )},
    "message": "The order of the epochs in the study design (as defined by the next attribute) does not match the order of epochs as referenced from scheduled activity instances in the main timeline that are ordered by default condition.",
    "variables": [
        "id",
        "name",
        "ScheduleTimeline.id",
        "ScheduleTimeline.name",
        "ScheduleTimeline.mainTimeline",
        "Epoch order",
        "Referenced epoch order"
    ],
    "scope": {
        "Include": [
            "StudyDesign",
            "InterventionalStudyDesign",
            "ObservationalStudyDesign"
        ]
    }
}