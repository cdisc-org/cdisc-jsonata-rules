{
    "check_func" : function($data){$data.(
                (
                    $d:=$;
                    ($.study.documentedBy)@$spd.
                        ($spd.versions)@$spdv.
                            ($spdv.contents[$contains(text,/usdm:ref/)])@$nc.
                                $match( $nc.text,
                                        /<usdm:ref([^>]*)(\/>|><\/usdm:ref>)/
                                )@$ref.
                                (
                                    $g0_or_null := function($m){$m ? $m.groups[0] : null};
                                    {
                                        "StudyProtocolDocument.id": $spd.id,
                                        "StudyProtocolDocument.name": $spd.name,
                                        "StudyProtocolDocumentVersion.id": $spdv.id,
                                        "StudyProtocolDocumentVersion.protocolVersion": $spdv.protocolVersion,
                                        "id": $nc.id,
                                        "name": $nc.name,
                                        "sectionNumber": $nc.sectionNumber,
                                        "sectionTitle": $nc.sectionTitle,
                                        "text": $nc.text,
                                        "usdm_ref": {
                                                        "match": $ref.match,
                                                        "klass": $match($ref.groups[0],/klass=\"([a-zA-Z]+)\"/) ~> $g0_or_null(),
                                                        "id": $match($ref.groups[0],/id=\"(\w+)\"/) ~> $g0_or_null(),
                                                        "attribute": $match($ref.groups[0],/attribute=\"([a-zA-Z]+)\"/) ~> $g0_or_null()
                                                    },                            
                                        "dataset": $nc.instanceType
                                    }
                                )
                                ~>  $map(function($v)
                                        {
                                            (
                                                $ref_val := "usdm_ref" in $keys($v)
                                                            ? $utils.get_ref_value($v.usdm_ref,$d,"!!NOT FOUND!!")
                                                            : $v.text;
                                                $v ~> |$|{"Invalid Reference": $v.usdm_ref.match, "Referenced Value": $ref_val},['usdm_ref']|
                                            )
                                        }
                                    )
                                ~>  $filter(function($v)
                                        {
                                            $v.`Referenced Value` = "!!NOT FOUND!!"
                                        }
                                    )
                )
            )},
    "message": "The item referenced in the narrative content text is not available elsewhere in the model.",
    "variables": [
            "StudyProtocolDocument.id",
            "StudyProtocolDocument.name",
            "StudyProtocolDocumentVersion.id",
            "StudyProtocolDocumentVersion.protocolVersion",
            "id",
            "name",
            "sectionNumber",
            "sectionTitle",
            "text",
            "Invalid Reference"
        ],
    "scope": {
        "Include": [
            "NarrativeContent"
        ]
    }
}