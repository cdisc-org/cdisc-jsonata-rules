{
    "check_func" : function($data){$data.(
        ($.**.studyDesigns)@$sd.
    $sd.population@$p.
    $p.
        [
            (
                $NullCohort:=("null" in cohorts.$type(plannedCompletionNumber) or ($count(cohorts.plannedCompletionNumber)!=$count(cohorts.id))); 
                $InCohort:=$boolean(cohorts.plannedCompletionNumber);      
                $InPop:=($type(plannedCompletionNumber) != "null" and $exists(plannedCompletionNumber));  
                $FValU:=function($n)
                    {
                        (
                            $exists($n)
                            ?   $type($n)="object"
                                ?   $exists($n.value)
                                   ? $n.value&$n.unit.standardCode.decode
                                   : $n.minValue.value&$n.minValue.unit.standardCode.decode&" to "&
                                     $n.maxValue.value&$n.maxValue.unit.standardCode.decode
                                : $string($n)
                            : "Missing"
                        )              
                    };
                    {
                        "instanceType": $p.instanceType,
                        "id": $p.id,
                        "path": $p._path,
                        "StudyDesign.id": $sd.id,
                        "StudyDesign.name": $sd.name,
                        "name": $p.name,
                        "plannedCompletionNumber": $FValU($p.plannedCompletionNumber),
                        "cohorts.name": "["& $join($p.cohorts.(id & ": " & name),", ") & "]",
                        "cohorts.plannedCompletionNumber": "["& $join($p.cohorts.(id & ": " & $FValU(plannedCompletionNumber)),", ") & "]",
                        "check": (($InPop=true and $InCohort=true) or ($InPop=false and $NullCohort=true and $InCohort=true))
                        }
            )
        ]
        [check=true]
           )},
    "message": "A planned completion number has been specified for both the study population and one or more cohorts, or it has been specified for only a subset of the cohorts.",
    "variables": 
        [
            "StudyDesign.id",
            "StudyDesign.name",
            "name",
            "plannedCompletionNumber",
            "cohorts.name",
            "cohorts.plannedCompletionNumber"
        ],
    "scope": {
        "Include": [
            "StudyDesignPopulation"
        ]
    }                   
}
