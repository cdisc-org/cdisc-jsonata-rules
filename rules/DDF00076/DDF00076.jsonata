{
    "check_func" : function($data){$data.(
  ($.study.versions)@$sv.
    [
      (
        $flatten:=function($tree) 
          {
            (
              $iter:=function($t, $p)
                {
                  $type($t) = "array"
                  ? [$map($t,function($v){$iter($v,$p)})]
                  : $type($t) = "object"
                    ? (
                        $pfx := ($p?($p & ">")) & $t.id & ($t.name?("["&$t.name&"]"));
                        $t ~> 
                        $sift(function($v, $k){$not($k in ["id","name"])}) ~>
                        $each(function($v){$iter($v,$pfx)})
                      )
                    : $join([$p,$t],">")
                };
              $iter($tree,"")
            )                        
          };
        $pcats := $utils.parse_refs("childIds","children",$sv.bcCategories);                
        ($sv.studyDesigns)@$sd.($sd.activities)@$a.
          ($a.biomedicalConceptIds)
            [
              $count($a.bcCategoryIds) > 0 and
              $ in [$a.bcCategoryIds.$lookup($pcats{id: **.memberIds},$)]
            ].
            (
              $this := $;
              {
                "instanceType": $a.instanceType,
                "id": $a.id,
                "path": $a._path,
                "StudyDesign.id": $sd.id,
                "StudyDesign.name": $sd.name,
                "name": $a.name,
                "biomedicalConceptId": $this,
                "bcCategoryId(s) containing BC": 
                  $utils.sift_tree($pcats[id in $a.bcCategoryIds],$this,["id","name"])~>$flatten
              }
            )     
      )
    ]
                    )},
    "message": "The activity references both a biomedical concept category and a biomedical concept, but the biomedical concept is a member of the referenced category or one of its subcategories.",
    "variables": [
        "StudyDesign.id",
        "StudyDesign.name",
        "name",
        "biomedicalConceptId",
        "bcCategoryId(s) containing BC"
    ],
    "scope": {
        "Include": [
            "Activity"
        ]
    }
}