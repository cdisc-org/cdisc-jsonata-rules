{
    "report": function($rule,$in_data)
                {
                    (
                        $add_path := function($obj) 
                                        {
                                            (
                                                $iter := function($o, $p)
                                                            {
                                                                $each($o, function($v1, $k) {
                                                                        {
                                                                            $k:
                                                                            $type($v1) = "object"
                                                                                ?   $iter($v1, ($p ? $p & "/" : "/") & $k ) 
                                                                                :   $type($v1) = "array" and $v1
                                                                                    ?  $map($v1, function($v2, $i) {
                                                                                                $type($v2) = "object" 
                                                                                                    ? $iter($v2, ($p ? $p & "/" : "/")  & $k & "/" & $i)
                                                                                                    : $v2
                                                                                            }
                                                                                        )[]
                                                                                    : $v1
                                                                        }
                                                                    }) ~> $append({"_path": $p}) ~> $merge()
                                                            };
                                                $iter($obj,"")
                                            )                   
                                        };
                        $data := $add_path($in_data);
                        $errors := $append(null,$type($rule.check_func) = "string" 
                                    ? $eval("($data:="&$string($data)&";$data.("&$rule.check_func&"))") 
                                    : $rule.check_func($data));
                        $rule_applies := function($instyp,$rs)
                                            {
                                                (
                                                    $incent := [$rs.Include];
                                                    $excent := [$rs.Exclude];
                                                    $is_inc := $instyp in $incent or "ALL" in $incent;
                                                    $is_exc := $instyp in $excent or "ALL" in $excent;
                                                    $is_inc and $not($is_exc)
                                                )
                                            };
                        $distinct($data.**.instanceType)@$it.($errors)
                            {
                                $it: 
                                    {
                                        "executionStatus": "success",
                                        "entity": $it[0]
                                    }
                            } 
                            ~> |*|
                                (
                                    $entity := $.entity;
                                    $rule_applies($entity,$rule.scope)
                                        ?
                                            $entity in $errors.instanceType 
                                                ? 
                                                    {
                                                        "message": $rule.message,
                                                        "variables": $rule.variables,
                                                        "errors" : $map($errors[instanceType = $entity],
                                                                        function($obj)
                                                                            {
                                                                                {
                                                                                    "id": $obj.id,
                                                                                    "path": $obj.path,
                                                                                    "value": $merge($map($rule.variables, function($var)
                                                                                                    {
                                                                                                        {
                                                                                                            $var: $var in $keys($obj) ? $lookup($obj,$var) : "Not in output"
                                                                                                        }
                                                                                                    }
                                                                                                )
                                                                                            )
                                                                                }
                                                                            }
                                                                    )[]
                                                    }
                                                : {}
                                        :   {
                                                "executionStatus": "skipped",
                                                "message": "Rule skipped - doesn't apply to entity for rule id=, dataset=" & $entity
                                            }
                                )|
                            ~> $spread() ~> $sort(function($l,$r){$keys($l)[0]>$keys($r)[0]}) ~> $merge()
                    )
                }
}